module Resource.Traits;

import Stdlib.Prelude open;
import Anoma.Resource open;
import Anoma.Transaction open;
import Anoma.Identity.External open;
import AnomaHelpers open;

import Resource.Traits.Convertable open public;
import Resource.Traits.Ephemerality open public;
import Resource.Traits.Kind open public;
import Resource.Traits.Label open public;
import Resource.Traits.Logic open public;
import Resource.Traits.Originator open public;
import Resource.Traits.Owner open public;
import Resource.Traits.Quantity open public;
import Resource.Traits.Supply open public;
import Resource.Traits.Transferability open public;

module FromConvertable;
  hasLogic {R} {{Convertable R}} : HasLogic R :=
    mkHasLogic@{
      get (typed : R) : Logic := HasLogic.get (Convertable.toResource typed);
      set (logic : Logic) (typed : R) : R :=
        HasLogic.set logic (Convertable.toResource typed) |> Convertable.fromResource
    };

  hasKind {R} {{Convertable R}} : HasKind R :=
    mkHasKind@{
      get (typed : R) : Kind := HasKind.get (Convertable.toResource typed)
    };

  hasQuantity {R} {{Convertable R}} : HasQuantity R :=
    mkHasQuantity@{
      get (typed : R) : Quantity := HasQuantity.get (Convertable.toResource typed);

      set (quantity : Quantity) (typed : R) : R :=
        HasQuantity.set quantity (Convertable.toResource typed) |> Convertable.fromResource
    };

  hasEphemerality {R} {{Convertable R}} : HasEphemerality R :=
    mkHasEphemerality@{
      get (typed : R) : Ephemerality := HasEphemerality.get (Convertable.toResource typed);

      set (ephemerality : Ephemerality) (typed : R) : R :=
        HasEphemerality.set ephemerality (Convertable.toResource typed) |> Convertable.fromResource
    };

  --- TODO Refactor. Ownership should be uncoupled from the nullifier key commitment.
  hasOwner {R} {{Convertable R}} : HasOwner R :=
    mkHasOwner@{
      get (typed : R) : ExternalIdentity := HasOwner.get (Convertable.toResource typed);

      set (owner : ExternalIdentity) (typed : R) : R :=
        HasOwner.set owner (Convertable.toResource typed) |> Convertable.fromResource
    };
end;
