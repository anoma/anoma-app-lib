module Transaction.Merge;

import Stdlib.Prelude open;
import Anoma open;

import Resource.Error open;
import Resource.Traits open;
import Transaction.Traits open;

--- Merges ;Convertable; typed ;Resource;s of the same ;Kind;.
--- The function returns a ;StandardError; if the ;Convertable; ;Resource;s
--- - have different ;Kind;
--- - are ;Transferability.NonTransferable;
--- - are not owned by calling ;KeyPair;.
merge
  {R}
  {{Convertable R}}
  {{Owner R}}
  {{HasTransferability R}}
  {{Quantity R}}
  {{HasKind R}}
  {{TransactionConvertable R R}}
  (self : KeyPair)
  (toMerge : List R)
  (receiver : PublicKey)
  : Result StandardError Transaction :=
  case toMerge of
    | nil :=
      throw
        mkInsufficientElementsError@{
          limit := 1;
          actual := 0
        }
    | t :: _ :=
      let
        myself : PublicKey := KeyPair.pubKey self;
        nk : PrivateKey := KeyPair.privKey self;
        kind : Kind := HasKind.get t;
        totalQuantity : Nat := listFor (acc := 0) (ti in toMerge) {Quantity.get ti + acc};
        merged := Owner.set receiver (Quantity.set totalQuantity t);
      in case HasTransferability.get t of
           | NonTransferable := throw mkNonTransferableError
           | Transferable :=
             case find ((/=) kind) (map HasKind.get toMerge) of
               | just otherKind :=
                 throw
                   mkInvalidKindError@{
                     expected := kind;
                     actual := otherKind
                   }
               | nothing :=
                 case find ((/=) myself) (map (Owner.get) toMerge) of
                   | just notMyself :=
                     throw
                       mkUnauthorizedError@{
                         expected := myself;
                         actual := notMyself
                       }
                   | nothing := ok (TransactionConvertable.toTransaction nk toMerge [merged]);
