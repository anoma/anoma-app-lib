module Transaction.Finalize;

import Stdlib.Prelude open;
import Anoma open;
import AnomaHelpers open;

import Authorization.Identities open;
import Resource.Error open;
import Resource.Traits open;
import Transaction.Traits open;

--- Finalizes a ;Convertable; typed ;Resource;, if the calling ;KeyPair; is the owner.
--- This requires an ephemeral resource to be created.
--- The function returns a ;StandardError; if
--- - the calling ;KeyPair; is not the owner
--- - the supply type is ;Fixed; in which case finalization is not supported.
--- - the supply type is ;Capped; which is not supported.
finalize
  {R}
  {{Convertable R}}
  {{Owner R}}
  {{HasSupply R}}
  {{HasEphemerality R}}
  {{TransactionConvertable R R}}
  (self : KeyPair)
  (toFinalize : R)
  : Result StandardError Transaction :=
  let
    myself : PublicKey := KeyPair.pubKey self;
    nk : PrivateKey := KeyPair.privKey self;
    owner : PublicKey := Owner.get toFinalize;
    ephResource := HasEphemerality.set Ephemerality.Ephemeral (Owner.set Zero.pubKey toFinalize);
  in if
    | owner /= myself :=
      throw
        mkUnauthorizedError@{
          expected := myself;
          actual := owner
        }
    | else :=
      case HasSupply.get toFinalize of
        | Unbound := ok (TransactionConvertable.toTransaction nk [toFinalize] [ephResource])
        | Capped :=
          throw
            mkDefaultError@{
              msg := "Tokens with capped supply are not supported yet."
            }
        | Fixed _ :=
          throw
            mkDefaultError@{
              msg := "Tokens with fixed supply cannot be finalized."
            };
