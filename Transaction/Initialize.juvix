module Transaction.Initialize;

import Stdlib.Prelude open;
import Anoma open;
import AnomaHelpers open;

import Resource.Error open;
import Resource.Traits open;
import Transaction.Traits open;
import Utils.Dummy.Transaction as Dummy open using {finalize};

--- Initializes a ;Convertable; typed ;Resource;.
--- For tokens with fixed supply, a dummy is required.
--- The function returns a ;StandardError; if
--- - a dummy is provided for tokens with ;Unbound; supply.
--- - no dummy is provided for tokens with ;Fixed; supply.
--- - the supply type is ;Capped; which is not supported.
initialize
  {R}
  {{Convertable R}}
  {{Owner R}}
  {{HasSupply R}}
  {{HasEphemerality R}}
  {{TransactionConvertable R R}}
  (self : KeyPair)
  (toInitialize : R)
  (maybeDummy : Maybe Resource)
  : Result StandardError Transaction :=
  let
    myself : PublicKey := KeyPair.pubKey self;
    nk : PrivateKey := KeyPair.privKey self;
    ephResource : R := HasEphemerality.set Ephemerality.Ephemeral (Owner.set myself toInitialize);
    tx : Transaction := TransactionConvertable.toTransaction nk [ephResource] [toInitialize];
  in case HasSupply.get toInitialize of
       | Unbound :=
         case maybeDummy of {
           | nothing := ok tx
           | just dummy :=
             throw
               mkDefaultError@{
                 msg := "A dummy resource is only required for resources with fixed supply."
               }
         }
       | Capped :=
         throw
           mkDefaultError@{
             msg := "Tokens with capped supply are not supported yet."
           }
       | Fixed nf :=
         case maybeDummy of
           | nothing :=
             throw
               mkDefaultError@{
                 msg :=
                   "A resource to consume must be provided whose nullifier is referenced in the fixed supply type."
               }
           | just dummy :=
             case nullifier dummy nk == nf of
               | false :=
                 throw
                   mkDefaultError@{
                     msg :=
                       "The provided resource's nullifier does not match the one referenced in the fixed supply type."
                   }
               | true :=
                 ok
                   compose@{
                     tx1 := tx;
                     tx2 :=
                       Dummy.finalize@{
                         self;
                         dummy
                       }
                   };
