module Transaction.Split;

import Stdlib.Prelude open;
import Anoma open;

import Resource.Error open;
import Resource.Traits open;
import Transaction.Traits open;

--- Splits a ;Convertable; typed ;Resource;.
--- The function returns a ;StandardError; if the ;Convertable; ;Resource;
--- - is ;Transferability.NonTransferable;
--- - is not owned by the calling ;KeyPair;
--- - or quantities do not balance.
split
  {R}
  {{Convertable R}}
  {{HasOwner R}}
  {{HasQuantity R}}
  {{HasTransferability R}}
  {{TransactionConvertable R R}}
  (self : KeyPair)
  (toSplit : R)
  (quantitiesAndReceivers : List (Pair Nat PublicKey))
  : Result StandardError Transaction :=
  let
    myself : PublicKey := KeyPair.pubKey self;
    nk : PrivateKey := KeyPair.privKey self;
    owner : PublicKey := HasOwner.get toSplit;
    toSplitResource := Convertable.toResource toSplit;
    sum : Nat := listFor (acc := 0) (quantity, _ in quantitiesAndReceivers) {quantity + acc};

    balance := HasQuantity.get toSplit;
    created : List R :=
      map (quantity, receiver in quantitiesAndReceivers)
        {toSplit |> HasOwner.set receiver |> HasQuantity.set quantity};
  in case HasTransferability.get toSplit of
       | NonTransferable := throw mkNonTransferableError
       | Transferable :=
         case owner == myself of
           | false :=
             throw
               mkUnauthorizedError@{
                 expected := myself;
                 actual := owner
               }
           | true :=
             case balance == sum of
               | false :=
                 throw
                   mkInsufficientHasQuantityError@{
                     limit := balance;
                     actual := sum
                   }
               | true := ok (TransactionConvertable.toTransaction nk [toSplit] created);
