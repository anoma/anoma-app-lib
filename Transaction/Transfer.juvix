module Transaction.Transfer;

import Stdlib.Prelude open;
import Anoma.Resource open;
import Anoma.Identity open;
import Anoma.Transaction open;

import Resource.Error open;
import Resource.Traits open;
import Transaction.Traits open;

--- Transfers a ;Convertable; resource type to a receiver, if the calling ;Identity; is the owner.
--- The function returns a ;StandardError; if the ;Convertable; ;Resource;
--- - is non-transferable
--- - is not owned by the calling ;Identity;.
transfer
  {R}
  {{Convertable R}}
  {{HasTransferability R}}
  {{HasOwner R}}
  {{TransactionConvertable R R}}
  (self : Identity)
  (toTransfer : R)
  (receiver : ExternalIdentity)
  : Result StandardError Transaction :=
  let
    myself : ExternalIdentity := Identity.external self;
    owner : ExternalIdentity := HasOwner.get toTransfer;
    transferred : R := HasOwner.set receiver toTransfer;
  in case HasTransferability.get toTransfer of
       | NonTransferable := throw mkNonTransferableError
       | Transferable :=
         case owner == myself of
           | false :=
             throw
               mkUnauthorizedError@{
                 expected := myself;
                 actual := owner
               }
           | true := ok (TransactionConvertable.toTransaction self [toTransfer] [transferred]);
