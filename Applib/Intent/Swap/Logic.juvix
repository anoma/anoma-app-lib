module Applib.Intent.Swap.Logic;

import Stdlib.Prelude open;
import Stdlib.Data.Set as Set open using {Set};
import Anoma.Identity open;
import Anoma.Resource open;
import Anoma.Transaction open;
import Anoma.Proving.Types open;

import Applib.Helpers open;
import Applib.Intent.Asset open;
import Applib.Resource.Traits open;
import Applib.Authorization.Identities open;

swapIntentLogic
  (want : QuantifiedAssets)
  (receiver : ExternalIdentity)
  (publicInputs : Logic.Instance)
  (privateInputs : Logic.Witness)
  : Bool :=
  let
    tag := Logic.Instance.tag publicInputs;
    customInputs := Logic.Witness.customInputs privateInputs;
  in case tag of
       | Logic.Consumed nullifier :=
         case lookupResource nullifier customInputs of {
           | nothing := false
           | just self :=
             case HasEphemerality.get self of {
               | Ephemeral := true
               | NonEphemeral := false
             }
         }
       | Logic.Created commitment :=
         case lookupResource commitment customInputs of
           | nothing := false
           | just self :=
             case HasEphemerality.get self of
               | NonEphemeral := true
               | Ephemeral :=
                 let
                   created := Logic.Witness.created privateInputs;
                 in case QuantifiedAssets.quantifier want of
                      | Any :=
                        any (asset in QuantifiedAssets.assets want)
                          includesAsset@{
                            asset;
                            receiver;
                            resources := created
                          }
                      | All :=
                        all (asset in QuantifiedAssets.assets want)
                          includesAsset@{
                            asset;
                            receiver;
                            resources := created
                          };
