module Utils.Counter.Extra;

import Stdlib.Prelude open;
import Data.Set as Set open using {Set};
import Anoma open;
import AnomaHelpers open;

import Authorization.Message open;
import Utils.Counter.Count open;

-- TODO use nullifier instead of commitment
findCreatedCounter (consumedCounter : Resource) (tx : Transaction) : Maybe Resource :=
  let
    cm : Commitment := commitment consumedCounter;
  in case
       lookupExtraData@{
         key := natToBytes32 (anomaEncode cm);
         Value := ResourceRelationship;
         tx
       }
     of
       | nothing := nothing
       | just msg :=
         case
           Set.toList (ResourceRelationship.mustBeConsumed msg)
             , Set.toList (ResourceRelationship.mustBeCreated msg)
         of
           | nil, [foundCm] :=
             let
               foundCounter : Resource := commitmentResource foundCm;
             in if
               | ResourceRelationship.origin msg == cm
                 && anomaKind consumedCounter == anomaKind foundCounter
                 && isCreated foundCounter tx := just foundCounter
               | else := nothing
           | _, _ := nothing;
