module Token.Resource;

import Stdlib.Prelude open;
import Anoma open;
import AnomaHelpers open;

import Resource.Traits open;
import Token.Label open;
import Token.Logic open;

module Token;
  type Token := mk {resource : Resource};

  create (quantity : Nat) (tokenLabel : Label) (npk : PublicKey) {eph : Bool := false} : Token :=
    mk
      Resource.mk@{
        logic := tokenLogic (Label.supply tokenLabel) (Label.originator tokenLabel);
        label := anomaEncode (tokenLabel);
        quantity;
        data := 0;
        eph;
        npk;
        nonce := rand;
        rseed := rand
      };

  open Token public;

end;

open Token using {Token} public;

instance
Token-Convertable : Convertable Token :=
  mkConvertable@{
    toResource (t : Token) : Resource := Token.resource t;
    fromResource (r : Resource) : Token := Token.mk r
  };

kindFromTokenLabel (tokenLabel : Label) : Kind :=
  Kind.fromLabelAndLogic@{
    logic := tokenLogic (Label.supply tokenLabel) (Label.originator tokenLabel);
    label := anomaEncode tokenLabel
  };

instance
Token-Quantity : Quantity Token := FromConvertable.quantity {Token};

instance
Token-Owner : Owner Token := FromConvertable.owner {Token};

instance
Token-HasEphemerality : HasEphemerality Token := FromConvertable.hasEphemerality {Token};

instance
Token-HasLogic : HasLogic Token := FromConvertable.hasLogic {Token};

instance
Token-HasKind : HasKind Token := FromConvertable.hasKind {Token};

instance
Token-Label-HasLabel : HasLabel Token Label :=
  mkHasLabel@{
    get (token : Token) : Label := token |> Convertable.toResource |> Resource.label |> anomaDecode;
    set (label : Label) (token : Token) : Token :=
      let
        resource := Convertable.toResource token;
      in resource@Resource{label := anomaEncode label} |> Convertable.fromResource
  };

instance
Token-HasSupply : HasSupply Token :=
  mkHasSupply@{
    get (token : Token) : Supply := token |> HasLabel.get {Token} {Label} |> Label.supply;
    set (supply : Supply) (token : Token) : Token :=
      let
        newLabel := (HasLabel.get {Token} {Label} token)@Label{supply := supply};
      in HasLabel.set {Token} {Label} newLabel token
  };

instance
Token-HasTransferability : HasTransferability Token :=
  mkHasTransferability@{
    get (token : Token) : Transferability :=
      token |> HasLabel.get {Token} {Label} |> Label.transferability;
    set (transferability : Transferability) (token : Token) : Token :=
      let
        newLabel := (HasLabel.get {Token} {Label} token)@Label{transferability := transferability};
      in HasLabel.set {Token} {Label} newLabel token
  };

instance
Token-HasOriginator : HasOriginator Token :=
  mkHasOriginator@{
    get (token : Token) : PublicKey := token |> HasLabel.get {Token} {Label} |> Label.originator;
    set (originator : PublicKey) (token : Token) : Token :=
      let
        newLabel := (HasLabel.get {Token} {Label} token)@Label{originator := originator};
      in HasLabel.set {Token} {Label} newLabel token
  };
