module Token.Label;

import Stdlib.Prelude open;
import Anoma open;

import Token.Supply open;

type Label :=
  mkLabel {
    name : Nat;
    symbol : String;
    decimals : Nat;
    supply : Supply
  };

instance
Label-Eq : Eq Label :=
  let
    toProd (l : Label) : _ :=
      Label.name l
        , Label.symbol l
        , Label.decimals l
        , Label.supply l;
  in mkEq@{
    eq (l1 l2 : Label) : Bool := toProd l1 == toProd l2
  };

instance
Label-Show : Show Label :=
  mkShow
    \ {l :=
      "{"
        ++str "name : "
        ++str (l |> Label.name |> Show.show)
        ++str ", "
        ++str "symbol : "
        ++str (l |> Label.symbol |> Show.show)
        ++str ", "
        ++str "decimals : "
        ++str (l |> Label.decimals |> Show.show)
        ++str ", "
        ++str "supply : "
        ++str (l |> Label.supply |> Show.show)
        ++str "}"};

getLabel (r : Resource) : Label :=
  anomaDecode (Resource.label r);

getName (r : Resource) : Nat := Label.name (getLabel r);

getSymbol (r : Resource) : String :=
  Label.symbol (getLabel r);

getDecimals (r : Resource) : Nat :=
  Label.decimals (getLabel r);

getSupply (r : Resource) : Supply :=
  Label.supply (getLabel r);

terminating
containsWrongLabel
  (labels : List Label) (label : Label) : Maybe Label :=
  case labels of
    | nil := nothing
    | l :: ls :=
      if
        | l /= label := just l
        | else := containsWrongLabel ls label;

type InvalidLabelError :=
  mkInvalidLabelError {
    expected : Label;
    actual : Label
  };

instance
InvalidLabelError-Show : Show InvalidLabelError :=
  mkShow
    \ {e :=
      "InvalidLabelError:\n{"
        ++str "expected : "
        ++str (e |> InvalidLabelError.expected |> Show.show)
        ++str ", "
        ++str "actual"
        ++str (e |> InvalidLabelError.actual |> Show.show)
        ++str "}"};
